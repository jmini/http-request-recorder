plugins {
    id 'org.asciidoctor.convert' version '1.5.9.1'
    id 'org.ajoberstar.git-publish' version '2.0.0'
    id 'net.researchgate.release' version '2.6.0'
}

wrapper {
    gradleVersion = '5.6.4'
}

allprojects {
    apply plugin: 'maven-publish'

    group = 'com.unblu.tools'
}

subprojects {
    ext {
        junitVersion = '5.7.1'
        logbackVersion = '1.2.3'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    publishing {
        repositories {
            maven {
                url = unbluBintrayReleases
                credentials {
                    username = project.findProperty("bintrayUser") ?: ""
                    password = project.findProperty("bintrayPassword") ?: ""
                }
            }
        }
    }
}


def buildDate() {
    return new Date().format('yyyy-MM-dd')
}

asciidoctor {
    sourceDir = file('docs')
    outputDir = file('build/docs')
    attributes = ['revdate'           : "${buildDate()}",
                  'project-version'   : "$version",
                  'gh-repo-owner'     : "$githubRepositoryOwner",
                  'gh-repo-name'      : "$githubRepositoryName",
                  'source-highlighter': 'coderay',
                  'imagesdir'         : '',
                  'toc'               : 'left',
                  'icons'             : 'font',
                  'sectanchors'       : 'true',
                  'idprefix'          : '',
                  'idseparator'       : '-',
                  'docinfo1'          : 'true']

    repositories {
        maven {
            url "http://dl.bintray.com/jmini/maven/"
        }
        jcenter()
    }
    dependencies {
        asciidoctor "fr.jmini.asciidoctorj:git-link:3.0.0"
    }
}

gitPublish {
    repoUri = 'git@github.com:' + "$githubRepositoryOwner" + '/' + "$githubRepositoryName" + '.git'
    branch = 'gh-pages'

    contents {
        from "${file('build/docs/html5')}"
    }

    preserve {
        include '.nojekyll'
    }

    commitMessage = "Update the 'gh-pages' branch."
}

release {
    buildTasks = ['releaseBuild']
}

task releaseBuild {
    dependsOn(
            'core:clean',
            'core:build',
            'core:publish',
            'cli:clean',
            'cli:build',
            'cli:publish',
            'asciidoctor',
            'gitPublishPush'
    )
}
